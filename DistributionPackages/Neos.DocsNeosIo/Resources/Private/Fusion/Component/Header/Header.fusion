prototype(Neos.DocsNeosIo:Component.Header) < prototype(Neos.Fusion:Component) {
    navigationRoot = false
    homeLink = Neos.Neos:NodeUri {
        node = ${site}
    }
    logoUri = Neos.Fusion:ResourceUri {
        path = 'resource://Neos.DocsNeosIo/Public/Frontend/img/neos_primary.svg'
    }

    renderer = afx`
        <header class="main-header">
            <div class="container">
                <a href="#" data-target="nav-mobile" class="top-nav sidenav-trigger circle hide-on-large-only">
                    <i class="material-icons">menu</i>
                </a>
            </div>
            <ul id="nav-mobile" class="sidenav sidenav-fixed">
                <li class="logo">
                    <a id="logo-container" href={props.homeLink} class="brand-logo">
                        <object id="front-page-logo" type="image/svg+xml" data={props.logoUri}>Neos CMS Docs</object>
                    </a>
                </li>
                <li class="search">
                    <div class="search-wrapper">
                        <Neos.DocsNeosIo:Component.SearchForm ajaxTarget=".main-header .search-results" />
                        <div class="search-results"></div>
                    </div>
                </li>
                <li>
                    <ul>
                        <Neos.DocsNeosIo:Component.Navigation startingPoint={props.navigationRoot} renderCurrentPath={true} />
                    </ul>
                </li>
            </ul>
        </header>
    `

    @cache {
        mode = 'cached'
        entryIdentifier {
            documentNode = ${documentNode}
        }
        entryTags {
            1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', documentNode)}
        }
    }
}
